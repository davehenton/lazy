// gradle-pitest-plugin stopped being compatible with the new plugin
// mechanism after version 1.1.4. For it, we use the old mechanism.
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.11'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'checkstyle'
    id 'findbugs'
    id 'pmd'
    // id 'info.solidsoft.pitest' version '1.1.4'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.1'
}

apply plugin: 'info.solidsoft.pitest'
apply plugin: 'io.codearte.nexus-staging'

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

group = 'com.thejohnfreeman'
version = '0.5.0'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.guava:guava:19.0'
    testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

checkstyle {
    toolVersion = '7.5'
    config = resources.text.fromFile('config/checkstyle.checks.xml')
}

pmdMain {
    consoleOutput = true
    ruleSetFiles = files('config/pmd.xml')
}

pmdTest {
    consoleOutput = true
    ruleSetFiles = files('config/pmd.xml')
}


test {
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

pitest {
    timestampedReports = false
    outputFormats = ['XML']
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
    required System.env.TRAVIS != "true"
}

// To release a new version:
// 1. Bump the version number in build.gradle
// 2. Commit the changes
// 3. Tag the commit with the version number, prefixed with "v", e.g. "v1.0.0"
// 4. Run `./gradlew uploadArchives closeAndPromoteRepository`
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'lazy'
                packaging 'jar'
                description 'A library for type-safe, tractable lazy evaluation and late binding in Java.'
                url 'https://github.com/thejohnfreeman/lazy'

                scm {
                    connection 'https://github.com/thejohnfreeman/lazy.git'
                    url 'https://github.com/thejohnfreeman/lazy'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://github.com/thejohnfreeman/lazy/blob/master/LICENSE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'thejohnfreeman'
                        name 'John Freeman'
                        email 'jfreeman@twosigma.com'
                    }
                }
            }
        }
    }
}
